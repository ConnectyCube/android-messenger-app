apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlinx-serialization'

android {
    compileSdk 31
    defaultConfig {
        applicationId "com.connectycube.messenger"
        minSdkVersion 18
        targetSdkVersion 31
        versionCode 5
        versionName "1.2.2"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
        javaCompileOptions {
            annotationProcessorOptions {
                arguments += [
                        "room.incremental": "true"]
            }
        }

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'),
                    'proguard-connectycube-sdk.pro',
                    'proguard-gson.pro',
                    'proguard-rules.pro',
                    'proguard-matisse.pro',
                    'proguard-glide.pro'
        }
    }

    // work-runtime-ktx 2.1.0 and above now requires Java 8
    kotlinOptions {
        jvmTarget = "1.8"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

ext {
    sdkVersion = '1.9.2'
    roomVersion = '2.4.0'
    lifecycleVersion = '2.2.0-alpha02'
    workVersion = '2.7.0'
    materialVersion = '1.1.0-alpha09'
    paging_version = "2.1.0"
    recyclerViewVersion = '1.1.0-beta03'
}

dependencies { configuration ->
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.5.1"

    connectycubeComponents(configuration, "2.0.0-beta05")
    androidJetPackComponents(configuration, roomVersion, lifecycleVersion, paging_version, workVersion)
    androidXComponents(configuration)
    kotlinComponents(configuration, kotlin_version)
    externalMediaComponents(configuration)

    implementation "com.google.android.material:material:$materialVersion"
    implementation 'com.jakewharton.timber:timber:4.7.1'

    implementation "com.google.android.gms:play-services-gcm:17.0.0"
    implementation "com.google.firebase:firebase-messaging:20.0.0"
    implementation "com.google.firebase:firebase-core:17.2.0"
    implementation "com.google.code.gson:gson:2.8.9"
    implementation "com.timehop.stickyheadersrecyclerview:library:0.4.3"
}


private static void connectycubeComponents(configuration, version) {
    configuration.implementation "com.connectycube.sdk:connectycube-android:$version"
}

private static void androidJetPackComponents(configuration, roomVersion, lifecycleVersion,
                                             paging_version, workVersion) {
    configuration.kapt "androidx.room:room-compiler:$roomVersion"
    configuration.implementation "androidx.room:room-runtime:$roomVersion"
    configuration.kapt "androidx.room:room-compiler:$roomVersion"
    configuration.implementation "androidx.lifecycle:lifecycle-extensions:$lifecycleVersion"
    configuration.implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycleVersion"
    configuration.implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycleVersion"
    configuration.implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycleVersion"
    configuration.implementation "androidx.paging:paging-runtime-ktx:$paging_version"
    configuration.implementation "androidx.work:work-runtime-ktx:$workVersion"
}

private static void androidXComponents(configuration) {
    configuration.implementation "androidx.multidex:multidex:2.0.1"
    configuration.implementation "androidx.activity:activity-ktx:1.1.0-alpha02"
    configuration.implementation "androidx.recyclerview:recyclerview:1.1.0-beta03"
    configuration.implementation 'androidx.appcompat:appcompat:1.0.2'
    configuration.implementation 'androidx.core:core-ktx:1.0.2'
    configuration.implementation 'androidx.constraintlayout:constraintlayout:2.0.0-beta3'
}

private static void kotlinComponents(configuration, kotlin_version) {
    configuration.implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
}

private static void externalMediaComponents(configuration) {
    configuration.implementation "com.zhihu.android:matisse:0.5.3-beta3"
    configuration.implementation 'com.github.yalantis:ucrop:2.2.4'
    configuration.implementation "com.github.bumptech.glide:glide:4.9.0"
    configuration.implementation 'com.github.chrisbanes:PhotoView:2.2.0'
}
